# Maintainer: Michael Hansen <zrax0111 gmail com>
# Contributor: Francisco Magalh√£es <franmagneto gmail com>
# Contributor: ava1ar <mail(at)ava1ar(dot)me>

pkgname=code-oss
pkgdesc='Microsoft Code for Linux, Open Source version (VSCode)'
pkgver=1.25.0
pkgrel=1
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/Microsoft/vscode"
license=('MIT')
makedepends=('npm' 'nodejs-lts-carbon' 'gulp' 'python2' 'git' 'yarn')
depends=('gtk3' 'gconf' 'libnotify' 'libxss' 'libxtst' 'libxkbfile' 'nss' 'alsa-lib')
conflicts=('code' 'code-oss-git')
options=(!strip)
source=("vscode-${pkgver}::git+https://github.com/Microsoft/vscode#tag=${pkgver}"
        'product_json.patch'
        'code-liveshare.patch')
sha256sums=('SKIP'
            '2a26fd93719970069da0a326b5ed77592234ffc6d05587b4d5bb8242c7f4c9b1'
            '90b8915d8195546088e845f3205fb965e941561d309c8b462bb0b22a159e041c')

case "$CARCH" in
    i686)
        _vscode_arch=ia32
        ;;
    x86_64)
        _vscode_arch=x64
        ;;
    armv7h)
        _vscode_arch=arm
        ;;
    *)
        # Needed for mksrcinfo
        _vscode_arch=DUMMY
        ;;
esac

prepare() {
    cd "${srcdir}"/vscode-${pkgver}

    # This patch no longer contains proprietary modifications.
    # See https://github.com/Microsoft/vscode/issues/31168 for details.
    patch -p1 -i "${srcdir}/product_json.patch"
    # See https://github.com/MicrosoftDocs/live-share/issues/262 for details
    patch -p1 -i "${srcdir}/code-liveshare.patch"
}

build() {
    cd "${srcdir}/vscode-${pkgver}"

    yarn install --arch=${_vscode_arch}

    # The default memory limit may be too low for current versions of node
    # to successfully build vscode.  Uncomment this to set it to 2GB, or
    # change it if this number still doesn't work for your system.
    mem_limit="--max_old_space_size=2048"

    if ! /usr/bin/node $mem_limit /usr/bin/gulp vscode-linux-${_vscode_arch}-min
    then
        echo
        echo "*** NOTE: If the build failed due to running out of file handles (EMFILE),"
        echo "*** you will need to raise your max open file limit."
        echo "*** This can be done by:"
        echo "*** 1) Set a higher 'nofile' limit (at least 10000) in either"
        echo "***    /etc/systemd/system.conf.d/limits.conf (for systemd systems)"
        echo "***    /etc/security/limits.conf (for non-systemd systems)"
        echo "*** 2) Reboot (or log out and back in)"
        echo "*** 3) Run 'ulimit -n' and ensure the value set above is shown before"
        echo "***    re-attempting to build this package."
        echo
        exit 1
    fi
}

package() {
    # Install main software bundle
    install -m 0755 -d "${pkgdir}"/usr/share/${pkgname}
    cp -r "${srcdir}"/VSCode-linux-${_vscode_arch}/* "${pkgdir}"/usr/share/${pkgname}

    # Create symlink to the startup script in /usr/bin
    install -m 0755 -d "${pkgdir}"/usr/bin
    ln -s /usr/share/${pkgname}/bin/${pkgname} "${pkgdir}"/usr/bin/${pkgname}

    # Create .desktop file
    install -m 0755 -d "${pkgdir}"/usr/share/applications
    sed -e "s|@@NAME_LONG@@|Microsoft Code OSS|g" \
        -e "s|@@NAME@@|${pkgname}|g" \
        -e "s|@@NAME_SHORT@@|Code - OSS|g" \
        -e "s|@@ICON@@|code|g" "${srcdir}"/vscode-${pkgver}/resources/linux/code.desktop \
        > "${pkgdir}"/usr/share/applications/${pkgname}.desktop

    # Install icon
    install -D -m644 "${pkgdir}"/usr/share/${pkgname}/resources/app/resources/linux/code.png \
            "${pkgdir}"/usr/share/pixmaps/code.png

    # Install license file
    install -D -m644 "${srcdir}"/vscode-${pkgver}/LICENSE.txt \
            "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
